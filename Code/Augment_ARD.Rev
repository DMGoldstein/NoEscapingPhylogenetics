data_path = " "
morph = readDiscreteCharacterData(data_path + "Chang_Augment.nex")


moves = VectorMoves()
monitors = VectorMonitors()

trees = readTrees(data_path + "ieo_hundred.nex") 

treetrace = treeTrace(trees)
psi ~ dnEmpiricalTree(treetrace)
moves.append( mvEmpiricalTree(psi, weight=10.0) )


map_tree = readTrees(data_path + "ChangA3.tree")[1]


n_gain ~ dnUniform(0,2)
moves.append( mvSlide(n_gain, delta=1.0, tune=true, weight=2.0) )
moves.append( mvSlide(n_gain, delta=0.1, tune=true, weight=2.0) )
moves.append( mvSlide(n_gain, delta=0.01, tune=true, weight=2.0) )


n_loss ~ dnUniform(4, 10)

moves.append( mvSlide(n_loss, delta=1.0, tune=true, weight=2.0) )
moves.append( mvSlide(n_loss, delta=0.1, tune=true, weight=2.0) )
moves.append( mvSlide(n_loss, delta=0.01, tune=true, weight=2.0) )

rate_pr_gain := psi.treeLength() / n_gain
rate_pr_loss := psi.treeLength() / n_loss


q_01 ~ dnExponential(rate_pr_gain)
q_10 ~ dnExponential(rate_pr_loss)
 

moves.append( mvScale( q_01, lambda=0.01,  weight=2 ) )
moves.append( mvScale( q_01, lambda=0.1,  weight=2 ) )
moves.append( mvScale( q_01, lambda=1, weight=2 ) )


moves.append( mvScale( q_10, lambda=0.01, weight=2 ) )
moves.append( mvScale( q_10, lambda=0.1, weight=2 ) )
moves.append( mvScale( q_10, lambda=1, weight=2 ) )


rates := [ [  0.0, q_01 ], 
			[ q_10, 0.0] ]


Q := fnFreeK( rates, rescaled=FALSE ) 



phyMorph ~ dnPhyloCTMC(tree=psi, Q=Q, type="Standard") 


phyMorph.clamp( morph )

mymodel = model ( Q )

n_gen = 100

monitors.append( mnModel(filename="output/Augment_ARD.log", printgen=n_gen) )

monitors.append( mnFile( filename="output/Augment_ARD.trees", printgen=n_gen, psi ) ) 

monitors.append( mnScreen(printgen=n_gen) )


monitors.append( mnJointConditionalAncestralState(tree=psi, ctmc=phyMorph, filename="output/Augment_ARD.states", type="Standard", printgen=n_gen, withStartStates=FALSE) )

mymcmc = mcmc(mymodel, monitors, moves, nruns=6, combine="mixed")

mymcmc.burnin(generations=20000, tuningInterval=200)

#Comment this out for the first run              
#mymcmc.initializeFromCheckpoint("output/Augment_ARD.state") 

mymcmc.run(generations=100000,checkpointInterval=1000,checkpointFile="output/Augment_ARD.state")

mymcmc.operatorSummary()



####################################
######Ancestral State Tree##########
####################################


ancestral_state_trace = readAncestralStateTrace("output/Augment_ARD.states") 


ancestral_state_tree_trace = readAncestralStateTreeTrace("output/Augment_ARD.trees") 

ancestral_state_tree = ancestralStateTree(tree=map_tree, include_start_states = FALSE, tree_trace=ancestral_state_tree_trace, ancestral_state_trace_vector=ancestral_state_trace, file="output/ASE_Augment_ARD.tree",  summary_statistic = "MAP", site=1)  

writeNexus(ancestral_state_tree, filename = "output/ASE_Augment_ARD.tree")



